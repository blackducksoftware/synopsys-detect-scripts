buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.blackducksoftware.integration:common-gradle-plugin:0.0.+'
        classpath 'com.blackducksoftware.integration:integration-rest:0.8.3'
        classpath 'com.blackducksoftware.integration:integration-common:15.4.0'
    }
}

group = 'com.synopsys.integration'
version = '1.0.2-SNAPSHOT'

apply plugin: 'com.blackducksoftware.integration.solution'

build {
    doLast {
        final File shellScriptTemplateFile = new File("${projectDir}/script-templates/detect-sh")
        final File shellScriptFile = new File("${buildDir}/detect.sh")
        buildScript(shellScriptTemplateFile, shellScriptFile)

        final File powershellScriptTemplateFile = new File("${projectDir}/script-templates/detect-ps")
        final File powershellScriptFile = new File("${buildDir}/detect.ps1")
        buildScript(powershellScriptTemplateFile, powershellScriptFile)

    }
}

import com.synopsys.integration.log.IntLogger
import com.synopsys.integration.log.Slf4jIntLogger
import com.synopsys.integration.rest.client.IntHttpClient
import com.synopsys.integration.rest.proxy.ProxyInfo
import com.synopsys.integration.rest.request.Request
import org.apache.commons.lang3.StringUtils
import org.slf4j.LoggerFactory

import java.nio.charset.StandardCharsets
import java.time.Instant

private final buildScript(final File scriptTemplateFile, final File outputFile) {
    final String VERSION_TOKEN = '//SCRIPT_VERSION//'
    final String BUILD_DATE_TOKEN = '//BUILD_DATE//'
    final String MAJOR_VERSIONS_TOKEN = '//DETECT_MAJOR_VERSIONS//'

    String scriptContents = scriptTemplateFile.getText('UTF-8')
    scriptContents = scriptContents.replaceAll(VERSION_TOKEN, version.toString())

    final Date date = Date.from(Instant.now())
    scriptContents = scriptContents.replaceAll(BUILD_DATE_TOKEN, date.format("YYYY-MM-dd"))

    final String majorVersionsCommentBlock = formatDetectPropertyTags(fetchDetectPropertyTags())
    scriptContents = scriptContents.replace(MAJOR_VERSIONS_TOKEN, majorVersionsCommentBlock)

    outputFile.delete()
    outputFile << scriptContents
    outputFile.setExecutable(true)
}

private final String formatDetectPropertyTags(final List<String> detectPropertyTags) {
    final int MAX_COMMENT_CHARACTERS = 55
    final StringBuilder result = new StringBuilder()

    String line = "#"
    for (int i = 0; i < detectPropertyTags.size(); i++) {
        final String tag = detectPropertyTags.get(i)
        String newText = " $tag"

        final boolean nextTagExists = i + 1 < detectPropertyTags.size()
        if (nextTagExists) {
            newText += ","
        }

        if (line.size() + newText.size() < MAX_COMMENT_CHARACTERS) {
            line = line + newText
        } else {
            result.append(line)
            result.append(System.lineSeparator())
            line = "#" + newText
        }
    }

    if (StringUtils.isNotBlank(line)) {
        result.append(line)
    }

    return result.toString()
}

private final List<String> fetchDetectPropertyTags() {
    final String artifactoryUrl = "https://repo.blackducksoftware.com/artifactory/api/storage/bds-integrations-release/com/blackducksoftware/integration/hub-detect?properties"

    final IntLogger intLogger = new Slf4jIntLogger(LoggerFactory.getLogger("fetchDetectPropertyTagLines"))
    final IntHttpClient intHttpClient = new IntHttpClient(intLogger, 200, true, ProxyInfo.NO_PROXY_INFO)
    final Request request = new Request.Builder().uri(artifactoryUrl).build()

    final List<String> propertyTags = new ArrayList<>()
    intHttpClient.execute(request).withCloseable { response ->
        final String responseContent = response.getContentString(StandardCharsets.UTF_8)
        final List<String> tags = responseContent.findAll("\"DETECT_LATEST.*?\"")
        for (final String tag : tags) {
            propertyTags.add(tag.replace("\"", ""))
        }
    }

    return propertyTags
}